grammar com.telluriumlang.Tellurium with org.eclipse.xtext.common.Terminals

generate tellurium "https://telluriumlang.github.io/tellurium"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

AutomationTestSet:
	testConfig += TestConfigurationStatement*
	testcases += TestCase*
;

TestConfigurationStatement:
	DriverConfig | DriverImplicityWait
;

DriverConfig:
	'using' type=DriverType 'driver' ';'
;

DriverImplicityWait:
	'driver' 'implicitly' 'wait' waitsec=INT 's' ';'
;

enum DriverType:
	HtmlUnit | Chrome | IE | Edge | Firefox | Safari
;

TestCase:
	'test' name=ID '{' statements+=TestStatement* '}'
;

TestStatement:
	OpenPage | Navigate | Window | QuitAndClose | KeyboardInput | MouseInput | Cookie | 
	VariableDeclaration
;

OpenPage:
	'open' target=Variables
;

Navigate:
	'navigate' action=NavigateAction
;

enum NavigateAction:
	back | forward | refresh
;

Window:
	'window' action=WindowActions
;

enum WindowActions:
	maximize | fullscreen
;

QuitAndClose:
	action= QuitAndCloseAction
;

enum QuitAndCloseAction:
	quit | close
;

KeyboardInput :
	SimpleKeyboardInput | ComplexKeyboardInput
;

SimpleKeyboardInput :
	'enter' keySequence = STRING ( 'into' target = ElementReferences )?
;

ComplexKeyboardInput :
	'enter' modifier = ModifierKey "+" keySequence = STRING ( 'into' target = ElementReferences )?
;

enum ModifierKey :
	shift | alt | ctrl | meta | win
;

MouseInput :
	'mouse' mAction = MouseAction ( 'on' target = ElementReferences )?
;

enum MouseAction :
	click | doubleClick | clickAndHold | rightClick | release
;

Cookie :
	'cookie' action=CookieAction
;

CookieAction :
	CookieAdd | CookieDelete
;

CookieAdd :
	'add' key = STRING 'with' 'value' value = STRING
;

CookieDelete :
	{CookieDelete} 'delete' ( key = STRING | 'all' )
;

VariableDeclaration:
	 "let" name=ID "=" value=Variables
;

Variables:
	DoubleLitera | IntLitera | StringLitera | VarExpression | 
	ElementExpressions | ExtractElementFromList
;

DoubleLitera:
	val = Double
;

IntLitera:
	val = INT
;

StringLitera:
	val = STRING
;

VarExpression:
	var = [VariableDeclaration]
;

Double returns ecore::EDouble hidden():
	INT? "." INT
;

ElementExpressions:
	FindElement | FindElements
;

// Find one element, by using $('...') expression
FindElement:
	"$(" selector=STRING ")" 
;

// Find a list of elememts, by using $['...'] expression
FindElements:
	"$[" selector=STRING "]" 
;

// Get an element with explicit index from a Web Element List
ExtractElementFromList:
	operation=ElementsListOperation "[" index=Variables "]"
;

// Two types of definitions for the elements list 
ElementsListOperation:
	ElementsSelectorExpr | ElementsSelectorRef
;

// Directly use the $("...") expression to locate elements
ElementsSelectorExpr:
	elements=FindElements 
;

// Refer to an existing Element List 
ElementsSelectorRef:
	ref=[VariableDeclaration]
;

// Reference the Web Elements
ElementReferences:
	LocateElement | ElementReference | ExtractEleFromListRef
;

// Directly use the $("...") expression to locate the element
LocateElement:
	element=FindElement
;

// Cross reference to a WebElement
ElementReference:
	ref=[VariableDeclaration]
;

// Use an element from an existing Web Element List
ExtractEleFromListRef:
	extractRef=ExtractElementFromList
;